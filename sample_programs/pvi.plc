/**
 * This program reads input and stores it in the mounted filesystem's raw file.
 */

def <init>
{
  const ESC = 0x1b;
  const COLS = 8;
  const ROWS = 2;
  const LINES = "---";
  x = 0;
  y = 0;
  a = 0;
}

def <fsprint>
{
  ch = pop();
  while(ch[0] != 0)
    {
      fputch(ch[0]);
      ch = ch + 1;
    }
}

def <write_filename>
{
  if(argc == 1)
    {
      return;
    }

  fputch(0xa);
  push(LINES);
  call <fsprint>;

  push(argv[1]);
  call <fsprint>;
  
  push(LINES);
  call <fsprint>;
  fputch(0xa);
  fflush();
}

def <sig_action>
{
  clear();
  if(argc > 1)
    {
      call <write_filename>;
    }
  unlock();
  sprint("DONE!");
  exit(0);
}

def <adjust_coords>
{
  if(x == COLS)
    {
      y = y + 1;
      x = 0;
    }
  if(y == ROWS)
    {
      y = 0;
    }
}

def <insert_loop>
{
  a = getch();
  while(a != ESC)
    {
      fputch(a);
      putch(a);
      a = getch();
    }
}

def <do_quit>
{
  a = getch();
  while(a != ESC)
    {
      if(a == 0xa)
	{
	  call <sig_action>;
	}
      putch(a);
      a = getch();
    }
}

def <command_loop>
{
  clear();
  putch(':');
  a = getch();
  while( a != ESC)
    {
      if(a == 0xa)
	{
	  break;
	}
      putch(a);

      switch(a)
	{
	case 'q':
	  {
	    call <do_quit>;
	    break;
	  }
	case 'w':
	  {
	    fflush();
	    break;
	  }
	}
      a = getch();
    }
  clear();
  rawload();
  sprint(fin);
}

def <main>
{
  signal(SIGINT,<sig_action>);
  lock();
  call <init>;
  if(argc > 1)
    {
      call <write_filename>;
    }
  a = getch();
  fclear();
  while(a != 4)
    {
      switch(a)
	{
	case 'i':
	  {
	    call <insert_loop>;
	    break;
	  }
	case 'h':
	  {
	    x = getx();
	    y = gety();
	    if(x == 0)
	      {
		y = ROWS - 1;
		x = COLS - 1;
	      }
	    else
	      {
		x = x - 1;
		call <adjust_coords>;
	      }
	    move(x,y);
	    break;
	  }
	case 'j':
	  {
	    x = getx();
	    y = gety() + 1;
	    call <adjust_coords>;
	    move(x,y);
	    break;
	  }
	case 'k':
	  {
	    x = getx();
	    y = gety();
	    if( y == 0)
	      {
		y = ROWS - 1;
	      }
	    else
	      {
		y = y - 1;
	      }
	    call <adjust_coords>;
	    move(x,y);
	    break;
	  }
	case 'l':
	  {
	    x = getx() + 1;
	    y = gety();
	    call <adjust_coords>;
	    move(x,y);
	    break;
	  }
	case ':':
	  {
	    call <command_loop>;
	    break;
	  }
	}
      a = getch();
    }
  call <sig_action>;
}
