/**
 * This program reads input and stores it in the mounted filesystem's raw file.
 */

def <init>
{
  const ESC = 0x1b;
  const COLS = 8;
  const ROWS = 2;
  const LINES = "---";
  x = 0;
  y = 0;
  a = 0;
}

def <fsprint>
{
  ch = pop();
  while(ch[0] != 0)
    {
      fputch(ch[0]);
      ch = ch + 1;
    }
}

def <write_filename>
{
  if(argc == 1)
    {
      return;
    }

  cr();
  push(LINES);
  call <fsprint>;

  push(argv[1]);
  call <fsprint>;
  
  push(LINES);
  call <fsprint>;
  cr();
  fflush();
}

def <sig_action>
{
  clear();
  if(argc > 1)
    {
      call <write_filename>;
    }
  unlock();
  sprint("DONE!");
  putd(argc);
  exit(0);
}

def <adjust_coords>
{
  if(x == COLS)
    {
      y = y + 1;
      x = 0;
    }
  if(y == ROWS)
    {
      y = 0;
    }
}

def <insert_loop>
{
  a = getch();
  while(a != ESC)
    {
      fputch(a);
      putch(a);
      a = getch();
    }
}

def <do_quit>
{
  a = getch();
  while(a != ESC)
    {
      if(a == 0xa)
	{
	  call <sig_action>;
	}
      putch(a);
      a = getch();
    }
}

def <command_loop>
{
  clear();
  putch(':');
  a = getch();
  while( a != ESC)
    {
      if(a == 0xa)
	{
	  break;
	}
      putch(a);

      if(a == 'q')
	{
	  call <do_quit>;
	  break;
	}
      if(a == 'w')
	{
	  fflush();
	}
      a = getch();
    }
  clear();
  rawload();
  sprint(fin);
}

def <main>
{
  signal(SIGINT,<sig_action>);
  lock();
  call <init>;
  if(argc > 1)
    {
      call <write_filename>;
    }
  a = getch();
  fclear();
  while(a != 4)
    {
      if (a == 'i')
	{
	  call <insert_loop>;
	}
      if(a == 'h')
	{
	  x = getx();
	  y = gety();
	  if(x == 0)
	    {
	      y = ROWS - 1;
	      x = COLS - 1;
	    }
	  else
	    {
	      x = x - 1;
	      call <adjust_coords>;
	    }
	  move(x,y);
	}
      if(a == 'j')
	{
	  x = getx();
	  y = gety() + 1;
	  call <adjust_coords>;
	  move(x,y);
	}
      if(a == 'k')
	{
	  x = getx();
	  y = gety();
	  if( y == 0)
	    {
	      y = ROWS - 1;
	    }
	  else
	    {
	      y = y - 1;
	    }
	  call <adjust_coords>;
	  move(x,y);
	}
      if(a == 'l')
	{
	  x = getx() + 1;
	  y = gety();
	  call <adjust_coords>;
	  move(x,y);
	}
      if(a == ':')
	{
	  call <command_loop>;
	}
      a = getch();
    }
  call <sig_action>;
}
